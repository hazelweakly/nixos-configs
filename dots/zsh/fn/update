#!/usr/bin/env zsh

cleanup() {
  rm -f "$HOME"/src/personal/nixos-configs/result
}
trap cleanup EXIT INT TERM

() {
  echo "Rebuilding"

  export XDG_CACHE_HOME="$HOME/.cache"
  export XDG_CONFIG_HOME="$HOME/.config"
  export XDG_DATA_HOME="$HOME/.local/share"
  export XDG_STATE_HOME="$HOME/.local/state"

  if (( ${+commands[cachix]} )) && [[ -n "$CACHIX_AUTH_TOKEN" ]] ; then
    echo "pushing to cachix"
    push_cmd=( "cachix" "watch-exec" "hazel-nix-configs" "--" )
  fi

  if [ -f /etc/os-release ]; then
    "${push_cmd[@]}" sudo nixos-rebuild \
      --flake "$HOME"'/src/personal/nixos-configs#'"$(hostname)" \
      ${1+"$@"} switch
    return
  fi

  cd "$HOME"/src/personal/nixos-configs >/dev/null || return

  if (( ${+commands[darwin-rebuild]} )) ; then
    "${push_cmd[@]}" darwin-rebuild --flake "$HOME"/src/personal/nixos-configs ${1+"$@"} switch
  else
    nix() {
      command nix --extra-experimental-features 'nix-command flakes' "$@"
    }

    "${push_cmd[@]}" nix build .#darwinConfigurations."${"$(hostname)"%\.local}".system --keep-going ${1+"$@"}
    ./result/sw/bin/darwin-rebuild --flake "$HOME"/src/personal/nixos-configs ${1+"$@"} switch
  fi

  kitty_macos_bin=/Applications/kitty.app/Contents/MacOS/kitty
  if [[ ! -e "$kitty_macos_bin" ]] || [[ "${$(kitty --version):0:13}" != "${$("$kitty_macos_bin" --version):0:13}" ]]; then
    curl -L https://sw.kovidgoyal.net/kitty/installer.sh | sh /dev/stdin
  fi
}
