zstyle ':z4h:'                auto-update      'no'
zstyle ':z4h:'                start-tmux       'none'
zstyle ':z4h:'                prompt-at-bottom 'no'
zstyle ':z4h:bindkey'         keyboard         'pc'
zstyle ':z4h:autosuggestions' forward-char     'accept'
zstyle ':z4h:fzf-complete'    recurse-dirs     'yes'
zstyle ':z4h:ssh:*'           enable           'no'
zstyle ':z4h:'                propagate-cwd    yes
zstyle ':z4h:direnv'          enable           'yes'
zstyle ':z4h:direnv:success'  notify           'no'

# Some of this is not documented
zstyle ':z4h:homebrew-command-not-found' channel 'none'
zstyle ':z4h:fzf' channel command "ln -s -- $__fzf_dir \$Z4H_PACKAGE_DIR"
function -z4h-postinstall-fzf() {}
zstyle ':z4h:Tarrasch/zsh-bd' postinstall "command cp \$Z4H_PACKAGE_DIR/{,zsh-}bd.plugin.zsh"
zstyle ':z4h:chisui/zsh-nix-shell' postinstall "command cp \$Z4H_PACKAGE_DIR/{,zsh-}nix-shell.plugin.zsh"

z4h install chisui/zsh-nix-shell
z4h install hlissner/zsh-autopair
z4h install Tarrasch/zsh-bd

# Leave this for a day or so until all the persistent caching issues go away
export HISTFILE="${XDG_CACHE_HOME}/.zsh_history"
z4h init || return

() { # remove zsh4human paths
  # When I wrote this, only the gods and I understood it
  # and now, who knows?
  path=(${path:#${~${(j:|:)@}}})
} {$Z4H/zsh4humans/zb,$Z4H/fzf/bin}

() { # Move nix paths to the front of the first non nix-store paths and prune non existing entries
  local idx=$((${path[(i)/(usr|Library|bin|sbin)/*]} - 1))
  path=(${path[1,idx]} $@ ${path[idx,-1]})
  path=(${(@)^path}(-FN))
} {/run/current-system/sw/bin,~/.nix-profile/bin,/etc/profiles/per-user/$USER/bin,/nix/var/nix/profiles/default/bin}

() { # remove non-existing directories
  fpath=(${(@)^fpath}(-FN))
}

typeset -gaUr _initial_fpath=($fpath)
munge_xdg_data_dirs() {
  # Might have to postinstall hack into some core zsh4humans thingy to sed
  # replace some bullshit and get myself this function in the right location
  # before compinit is called for the first time?

  set -- "${(s.:.)^XDG_DATA_DIRS}"{/zsh/site-functions,/zsh/$ZSH_VERSION/functions,/zsh/vendor-completions}
  # remove non-existing directories, add existing XDG_DATA_DIRS
  [[ -n "$IN_NIX_SHELL" ]] && fpath=($@(-FN) ${(@)^fpath}(-FN)) && -z4h-compinit
  [[ -z "$IN_NIX_SHELL" ]] && fpath=($_initial_fpath) && -z4h-compinit
}
# add-zsh-hook -- precmd munge_xdg_data_dirs

z4h load -c -- chisui/zsh-nix-shell hlissner/zsh-autopair Tarrasch/zsh-bd

[ -d "$HOME/.local/bin" ] && path+=($HOME/.local/bin)
fpath+=("$ZDOTDIR/config/completions" "$ZDOTDIR/config/fn")
z4h source -c -- ${ZDOTDIR}/config/{30-prompt,31-prompt,80-exports}.zsh

z4h bindkey z4h-backward-kill-word  Ctrl+Backspace Ctrl+H
z4h bindkey z4h-backward-kill-zword Ctrl+Alt+Backspace Ctrl+W
z4h bindkey fzf-cd-widget Alt+C
bindkey '^[D' z4h-kill-zword
z4h bindkey fzf-file-widget Ctrl+T
z4h bindkey backward-kill-line Ctrl+U
z4h bindkey z4h-eof Ctrl+D
z4h bindkey undo Ctrl+/
z4h bindkey redo Alt+/

alias cat='bat --style full'
alias ls='exa --group-directories-first --icons --sort time'
alias l='exa --group-directories-first --icons --sort time -alg'
alias ll='exa --group-directories-first --icons --sort time -lg'
alias bc='bc -l'

hash -d dots=~/src/personal/nixos-configs
hash -d src=~/src

chpwd_functions=(${chpwd_functions[@]} "list_all")

autoload -Uz -- zmv ${ZDOTDIR}/config/fn/[^_]*(.)

setopt glob_dots
setopt no_auto_menu
setopt ignore_eof
setopt nonomatch
setopt no_flow_control

# Leave this for a day or so until all the persistent caching issues go away
export HISTFILE="${XDG_CACHE_HOME}/.zsh_history"
